#version 330 core
out vec4 FragColor;

in vec2 TexCoords;

uniform sampler2D image;    //应该是我们要采样的图像

uniform bool horizontal;

//以像素为中心，向左和向右(水平方向)的像素的权重
uniform float weight[5] = float[] (0.2270270270, 0.1945945946, 0.1216216216, 0.0540540541, 0.0162162162);

void main(){
    //textureSize(sampler, lod)函数:检索lod级别的sampler的图像的尺寸
    vec2 tex_offset = 1.0 / textureSize(image, 0); // 得到单个纹素(texel)的大小
    vec3 result = texture(image, TexCoords).rgb * weight[0];    //最开始是像素本身

    if(horizontal){ //如果确认是水平方向的
        //就开始水平方向的遍历像素
        for(int i = 1; i < 5; i++)
        {
            result += texture(image, TexCoords + vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
            result += texture(image, TexCoords - vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
        }
    }
    else    //垂直方向
    {
        //就开始垂直方向的遍历像素
        for(int i = 1; i < 5; i++){
            result += texture(image, TexCoords + vec2(0.0, tex_offset.y * i)).rgb * weight[i];
            result += texture(image, TexCoords - vec2(0.0, tex_offset.y * i)).rgb * weight[i];
        }
    }

    FragColor = vec4(result, 1.0);
}